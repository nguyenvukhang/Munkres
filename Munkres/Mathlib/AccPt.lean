import Munkres.Defs.Countable

open Filter Set Munkres
open scoped Topology

universe u v

variable {Î± : Type u} [TopologicalSpace Î±]
  {Î² : Type v}
  {A : Set Î±} {x : Î±}

/-- Munkres defines that x is a limit point of A if every open U âŠ† X containing
x intersects with A \ {x}. This is equivalent to Mathlib's `AccPt x (ğ“Ÿ A)`. -/
protected theorem AccPt.iff {x : Î±} : AccPt x (ğ“Ÿ A) â†” âˆ€ U âˆˆ nhds' x, (U âˆ© (A \ {x})).Nonempty
  := by --
  rw [accPt_principal_iff_clusterPt, clusterPt_principal_iff]
  simp only [mem_nhds_iff]
  refine âŸ¨fun h U âŸ¨hU, hxUâŸ© â†¦ (h U âŸ¨U, le_refl _, hU, hxUâŸ©), ?_âŸ©
  intro h S âŸ¨U, hUS, hU, hxUâŸ©
  let âŸ¨t, htU, htA, hneâŸ© := h U âŸ¨hU, hxUâŸ©
  exact âŸ¨t, hUS htU, htA, hneâŸ© -- âˆ

--* closure A = A âˆª A'
theorem AccPt.union_eq_closure : A âˆª { x | AccPt x (ğ“Ÿ A)} = closure A
  := by --
  refine le_antisymm ?_ ?_
  Â· intro x hx
    rcases hx with h | (h : AccPt x (ğ“Ÿ A))
    Â· exact subset_closure h
    Â· rw [AccPt.iff] at h
      rw [mem_closure_iff]
      intro U hU hxU
      obtain âŸ¨t, htU, htA, htxâŸ© := h U âŸ¨hU, hxUâŸ©
      exact âŸ¨t, htU, htAâŸ©
  Â· intro x hx
    refine or_iff_not_imp_left.mpr fun h â†¦ ?_
    simp
    refine AccPt.iff.mpr ?_
    intro U âŸ¨hU, hxUâŸ©
    rw [mem_closure_iff] at hx
    obtain âŸ¨y, hyU, hyAâŸ© := hx U hU hxU
    have : y â‰  x := ne_of_mem_of_not_mem hyA h
    refine âŸ¨y, hyU, hyA, thisâŸ© -- âˆ

theorem AccPt.mem_closure (h : AccPt x (ğ“Ÿ A)) : x âˆˆ closure A
  := by --
  exact mem_closure_iff_clusterPt.mpr h.clusterPt -- âˆ

-- Alternative proof.
example (h : AccPt x (ğ“Ÿ A)) : x âˆˆ closure A
  := by --
  have : x âˆˆ A âˆª { x | AccPt x (ğ“Ÿ A) } := Set.mem_union_right A h
  rw [AccPt.union_eq_closure] at this
  exact this -- âˆ

theorem AccPt.of_tendsto [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ Î±}
  (hA : âˆ€á¶  n in atTop, f n âˆˆ A) (htt : Tendsto f atTop (ğ“[â‰ ] x))
  : AccPt x (ğ“Ÿ A)
  := by --
  rw [AccPt.iff]
  intro U âŸ¨hU, hxUâŸ©
  rw [tendsto_nhdsWithin_iff] at htt
  obtain âŸ¨htt, hneâŸ© := htt
  rw [tendsto_atTop_nhds] at htt
  rw [eventually_atTop] at hne hA
  specialize htt U hxU hU
  obtain âŸ¨Nâ‚, httâŸ© := htt
  obtain âŸ¨Nâ‚‚, hneâŸ© := hne
  obtain âŸ¨Nâ‚ƒ, hAâŸ© := hA
  let N := (Nâ‚ âŠ” Nâ‚‚) âŠ” Nâ‚ƒ
  have hNâ‚ : Nâ‚ â‰¤ N := le_sup_left.trans le_sup_left
  have hNâ‚‚ : Nâ‚‚ â‰¤ N := le_sup_right.trans le_sup_left
  have hNâ‚ƒ : Nâ‚ƒ â‰¤ N := le_sup_right
  specialize htt N hNâ‚
  specialize hne N hNâ‚‚
  specialize hA N hNâ‚ƒ
  exact âŸ¨f N, htt, hA, hneâŸ© -- âˆ

theorem AccPt.of_tendsto_nat {f : â„• â†’ Î±} (hA : âˆ€á¶  n in atTop, f n âˆˆ A)
  (htt : Tendsto f atTop (ğ“[â‰ ] x)) : AccPt x (ğ“Ÿ A)
  := by --
  exact AccPt.of_tendsto hA htt -- âˆ

-- And this is the reason why we need (ğ“[â‰ ] x) above, and not just (ğ“ x).
example [hâ‚€ : Nonempty Î±] : âˆƒ (A : Set Î±) (x : Î±) (f : â„• â†’ Î±),
  (âˆ€á¶  n in atTop, f n âˆˆ A) âˆ§ Tendsto f atTop (ğ“ x) âˆ§ Â¬AccPt x (ğ“Ÿ A)
  := by --
  let x := hâ‚€.some
  refine âŸ¨{x}, x, fun _ â†¦ x, ?_, ?_, ?_âŸ©
  Â· exact eventually_const.mpr rfl
  Â· exact tendsto_const_nhds
  Â· by_contra! h
    rw [AccPt.iff] at h
    specialize h univ âŸ¨isOpen_univ, trivialâŸ©
    rw [sdiff_self, bot_eq_empty, inter_empty] at h
    exact Set.not_nonempty_empty h -- âˆ

theorem AccPt.exists_tendsto [hâ‚ : FirstCountableTopology Î±]
  : AccPt x (ğ“Ÿ A) â†’ âˆƒ (f : â„• â†’ Î±), (âˆ€ n, f n âˆˆ A) âˆ§ Tendsto f atTop (ğ“ x)
  := by --
  intro hx
  rw [AccPt.iff] at hx
  rw [FirstCountableTopology.iff] at hâ‚
  specialize hâ‚ x
  obtain âŸ¨Î², hÎ²_countable, hÎ²xâŸ© := hâ‚
  haveI : Countable Î² := hÎ²_countable
  obtain âŸ¨B, hB_anti, hBâŸ© := hÎ²x.exists_antitone_eq_range
  let hÎ´ (n : â„•) := hx (B n) (hB.nhds' âŸ¨n, rflâŸ©)
  let f (n : â„•) : Î± := (hÎ´ n).some
  use f
  refine âŸ¨?_, ?_âŸ©
  Â· intro n
    obtain âŸ¨hfB : f n âˆˆ B n, hfA : f n âˆˆ A \ {x}âŸ© := (hÎ´ n).some_mem
    exact hfA.1
  Â· rw [tendsto_atTop_nhds]
    intro U hxU hU
    obtain âŸ¨b, âŸ¨N, heqâŸ©, hbUâŸ© := hB.exists_mem_subset' hU hxU
    subst heq
    use N
    intro n hn
    obtain âŸ¨hfB : f n âˆˆ B n, hfA : f n âˆˆ A \ {x}âŸ© := (hÎ´ n).some_mem
    exact hbU <| hB_anti hn hfB -- âˆ
